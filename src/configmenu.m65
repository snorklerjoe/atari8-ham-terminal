;
; Strings and logic for the config menu.
;

caps=32

.local

conftemp;
    .byte $00

; Menu items
config_menu_clock:
    jmp config_unimplemented

config_menu_keyclick:
    puts string_keyclick

    lda UI_KEY_CLICK
    eor #~00000001
    sta UI_KEY_CLICK

    printifeq 0,string_off
    printifeq 1,string_on

    lda #$9B
    jsr putch

    jsr config_update_keyclick

    jmp config_menu_success

config_menu_logfile:
    ; Prompt
    puts string_current_value
    puts LOG_FILE
    lda #$9B
    jsr putch
    puts string_new

    ; Get input, up to 14 chars + EOL + 0x00
    gets LOG_FILE, 14

    jmp config_menu_success
    
.local
config_menu_logtranslation:
    puts string_log_translation

    lda LOG_TRANSLATION

    ; Toggle
    cmp #00
    bne @00
    lda #16
    jmp @03
@00
    cmp #16
    bne @01
    lda #32
    jmp @03
@01
    cmp #32
    bne @03
    lda #00

@03
    sta LOG_TRANSLATION

    ; Print new value
    pha
    printifeq 00,string_light
    pla
    pha
    printifeq 16,string_heavy
    pla
    pha
    printifeq 32,string_off
    pla

    lda #$9B
    jsr putch

    jmp config_menu_success

config_menu_serdev:
    ; Prompt
    puts string_current_value
    puts SERIAL_DEVICE
    lda #$9B
    jsr putch
    puts string_new

    gets SERIAL_DEVICE, 6

    jmp config_menu_success

config_menu_serbaud:
    ; Prompt
    puts string_baud
    lda SERIAL_COMM_SPEC

    clc
    adc #CONFIG_ENUMS_SERIAL_COMM_SPEC_BAUD_INC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_BAUD_MASK


    sta conftemp
    lda SERIAL_COMM_SPEC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_BAUD_MASKINV
    ora conftemp
    sta SERIAL_COMM_SPEC

    lda SERIAL_COMM_SPEC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_BAUD_MASK
    aslconst CONFIG_ENUMS_SERIAL_COMM_SPEC_BAUD_SHIFT

    tax
    puts_offset CONFIG_ENUMS_SERIAL_COMM_SPEC_BAUD

    lda #$9B
    jsr putch

    jmp config_menu_success

config_menu_serstopbits:
    ; Prompt
    puts string_stopbits

    lda SERIAL_COMM_SPEC
    eor #CONFIG_ENUMS_SERIAL_COMM_SPEC_STOPBITS_MASK
    sta SERIAL_COMM_SPEC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_STOPBITS_MASK

    printifeq 0,CONFIG_ENUMS_SERIAL_COMM_SPEC_STOPBITS
    printifeq 128,CONFIG_ENUMS_SERIAL_COMM_SPEC_STOPBITS+4

    lda #$9B
    jsr putch

    jmp config_menu_success

config_menu_serflowctrl:
    ; Prompt
    puts string_flowc
    lda SERIAL_COMM_SPEC2

    clc
    adc #CONFIG_ENUMS_SERIAL_COMM_SPEC2_MONITOR_INC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC2_MONITOR_MASK


    sta conftemp
    lda SERIAL_COMM_SPEC2
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC2_MONITOR_MASKINV
    ora conftemp
    sta SERIAL_COMM_SPEC2

    lda SERIAL_COMM_SPEC2
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC2_MONITOR_MASK
    aslconst CONFIG_ENUMS_SERIAL_COMM_SPEC2_MONITOR_SHIFT

    tax
    puts_offset CONFIG_ENUMS_SERIAL_COMM_SPEC2_MONITOR

    lda #$9B
    jsr putch

    jmp config_menu_success

.local
config_menu_sertranslation:
    puts string_ser_translation

    lda SERIAL_TRANSLATION
    and #~00110000

    ; Toggle
    cmp #00
    bne ?00
    lda #16
    jmp ?03
?00
    cmp #16
    bne ?01
    lda #32
    jmp ?03
?01
    cmp #32
    bne ?03
    lda #00

?03
    sta conftemp
    lda SERIAL_TRANSLATION
    and #~11001111
    ora conftemp
    sta SERIAL_TRANSLATION
    lda conftemp

    lda conftemp

    ; Print new value
    pha
    printifeq 00,string_light
    pla
    pha
    printifeq 16,string_heavy
    pla
    pha
    printifeq 32,string_off
    pla

    lda #$9B
    jsr putch

    jmp config_menu_success

config_menu_lfappend:
    puts string_lfappend

    lda SERIAL_TRANSLATION
    eor #64
    sta SERIAL_TRANSLATION

    and #64

    printifeq 0,string_off
    printifeq 64,string_on

    lda #$9B
    jsr putch

    jsr config_update_keyclick

    jmp config_menu_success
    

config_menu_serword:
    puts string_wsize
    lda SERIAL_COMM_SPEC

    clc
    adc #CONFIG_ENUMS_SERIAL_COMM_SPEC_WORDSIZE_INC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_WORDSIZE_MASK


    sta conftemp
    lda SERIAL_COMM_SPEC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_WORDSIZE_MASKINV
    ora conftemp
    sta SERIAL_COMM_SPEC

    lda SERIAL_COMM_SPEC
    and #CONFIG_ENUMS_SERIAL_COMM_SPEC_WORDSIZE_MASK
    asrconst CONFIG_ENUMS_SERIAL_COMM_SPEC_WORDSIZE_SHIFT

    tax
    puts_offset CONFIG_ENUMS_SERIAL_COMM_SPEC_WORDSIZE

    lda #$9B
    jsr putch

    jmp config_menu_success

.local
config_menu_serinparity:
    puts string_input
    puts string_parity

    lda SERIAL_TRANSLATION
    and #~00001100

    ; Toggle
    cmp #00
    bne ?00
    lda #04
    jmp ?03
?00
    cmp #04
    bne ?01
    lda #08
    jmp ?03
?01
    cmp #08
    bne ?02
    lda #12
    jmp ?03
?02
    cmp #12
    bne ?03
    lda #00

?03
    sta conftemp
    lda SERIAL_TRANSLATION
    and #~11110011
    ora conftemp
    sta SERIAL_TRANSLATION
    lda conftemp

    ; Print new value
    pha
    printifeq 00,string_none
    pla
    pha
    printifeq 04,string_odd
    pla
    pha
    printifeq 08,string_even
    pla
    pha
    printifeq 12,string_clear
    pla

    lda #$9B
    jsr putch

    jmp config_menu_success

.local
config_menu_seroutparity:
    puts string_output
    puts string_parity

    lda SERIAL_TRANSLATION
    and #~00000011

    ; Toggle
    cmp #00
    bne ?00
    lda #01
    jmp ?03
?00
    cmp #01
    bne ?01
    lda #02
    jmp ?03
?01
    cmp #02
    bne ?02
    lda #03
    jmp ?03
?02
    cmp #03
    bne ?03
    lda #00

?03
    sta conftemp
    lda SERIAL_TRANSLATION
    and #~11111100
    ora conftemp
    sta SERIAL_TRANSLATION
    lda conftemp

    ; Print new value
    pha
    printifeq 00,string_none
    pla
    pha
    printifeq 01,string_odd
    pla
    pha
    printifeq 02,string_even
    pla
    pha
    printifeq 03,string_clear
    pla

    lda #$9B
    jsr putch

    jmp config_menu_success

config_menu_serecho:
    puts string_echo

    lda SERIAL_ECHO
    eor #~00000001
    sta SERIAL_ECHO

    printifeq 0,string_off
    printifeq 1,string_on

    lda #$9B
    jsr putch

    jmp config_menu_success

config_menu_save:
    jmp config_unimplemented

config_menu_help:
    jsr print_config_menu
    jmp config_menu_success

config_unimplemented:
    puts config_unimplemented_msg
    jmp config_menu_success


config_unimplemented_msg
    .byte "Config option unimplemented!",$9B,$00


; menu conditionals-- launches menu subroutine.
; Each manu item subroutine should end with #00 in A
config_menu_conditionals:
    cmp #67 ; C
    jmpeq config_menu_clock

    cmp #75 ; K
    jmpeq config_menu_keyclick

    cmp #76 ; L
    jmpeq config_menu_logfile

    cmp #78 ; N
    jmpeq config_menu_lfappend

    cmp #84 ; T
    jmpeq config_menu_logtranslation

    cmp #82 ; R
    jmpeq config_menu_serdev

    cmp #66 ; B
    jmpeq config_menu_serbaud

    cmp #83 ; S
    jmpeq config_menu_serstopbits

    cmp #70 ; F
    jmpeq config_menu_serflowctrl

    cmp #65 ; A
    jmpeq config_menu_sertranslation

    cmp #87 ; W
    jmpeq config_menu_serword

    cmp #73 ; I
    jmpeq config_menu_serinparity
    cmp #79 ; O
    jmpeq config_menu_seroutparity

    cmp #69 ; E
    jmpeq config_menu_serecho

    cmp #68 ; D
    jmpeq config_menu_save

    cmp #72 ; H
    jmpeq config_menu_help

    rts

; go here after sucesfully processing a menu item
config_menu_success:
    lda #00
    rts

; GETCH's and processes menu item
.local
config_menu:
    jsr GETCH

    jsr config_menu_conditionals
    cmp #00
    beq ?back
    
    sbc #caps  ; Try the lowercase version
    jsr config_menu_conditionals
    cmp #00
    bne print_no_such_option

?back
    rts

; RETURN prints the next section, q exits
print_config_menu
    puts CONFIG_MENU_TEXT

    jsr GETCH
    cmp #27 ; esc
    beq @exit_conf
    cmp #81 ; Q
    beq @exit_conf

    puts CONFIG_MENU_TEXT2
    jsr dl_setzero

    jsr GETCH
    cmp #27 ; esc
    beq @exit_conf
    cmp #81 ; Q
    beq @exit_conf

    puts CONFIG_MENU_TEXT3
    jsr dl_setzero

@exit_conf
    rts

print_no_such_option
    pha
    puts NO_SUCH_MENU_OPTION
    pla
    jsr putch
    puts NO_SUCH_MENU_OPTION_END
    rts



CONFIG_FILE_NAME
    .byte "D:CONFIG.BIN", $9B

NO_SUCH_MENU_OPTION
    .byte $9B,"No such menu option '",$00
NO_SUCH_MENU_OPTION_END
    .byte "'!",$9B,$9B,$00

CONFIG_MENU_TEXT
    .byte "Available configuration:", $9B
    .byte "------------------------", $9B,$9B
    .byte "(RETURN for next section)", $9B
    .byte $9B,
    .byte "User Interface Preferences:", $9B
    .byte " C - Clock on/off", $9B
    .byte " K - Key click on tx on/off", $9B
    .byte $9B
    .byte "Logging Preferences:", $9B
    .byte " L - Log file (or device)", $9B
    .byte " T - Log ASCII/ATASCII translation", $9B,$9B, $00
CONFIG_MENU_TEXT2
    .byte "Serial Connection Parameters:", $9B
    .byte " R - Device #", $9B
    .byte " B - Baud Rate", $9B
    .byte " S - Stop Bits", $9B
    .byte " F - Flow Control", $9B
    .byte " A - ASCII/ATASCII translation", $9B
    .byte " W - Word size (bits)", $9B
    .byte " I - Input Parity", $9B
    .byte " O - Output Parity", $9B
    .byte " N - AppeNd LF after EOL", $9B
    .byte " E - ECHO", $9B
    .byte $9B,$00
CONFIG_MENU_TEXT3
    .byte "Save / Restore from D:CONFIG.BIN", $9B
    .byte " D - SAVE config TO Disk", $9B
;    .byte " > - LOAD config FROM disk", $9B
    .byte $9B
    .byte "H - Show this configuration menu", $9B
    .byte $9B,$00

